USE [Analytics]
GO

/****** Object:  View [dbo].[vw_FLUID_PROPERTIES]    Script Date: 2/21/2024 12:54:05 PM ******/
SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO


CREATE OR ALTER VIEW [dbo].[vw_FLUID_PROPERTIES] AS
WITH GOR_RAW_CTE AS (
	SELECT		WellID, DataSource, SUM(OIL) AS CUM_OIL_3MO, SUM(GAS) AS CUM_GAS_3MO
	FROM		dbo.vw_PRODUCTION_PIVOT
	WHERE		DateRankAsc <= 3
	AND			Cadence = 'MONTHLY'
	GROUP BY	WellID, DataSource
),
WTR_CUT_RAW_CTE AS (
	SELECT		WellID, DataSource, SUM(OIL) AS CUM_OIL_6MO_TAIL, SUM(WATER) AS CUM_WATER_6MO_TAIL
	FROM		dbo.vw_PRODUCTION_PIVOT
	WHERE		DateRankDesc <= 6
	AND			DateRankAsc > 3
	AND			Cadence = 'MONTHLY'
	GROUP BY	WellID, DataSource
)
SELECT		ISNULL(G.WellID, W.WellID) AS WellID, ISNULL(G.DataSource, W.DataSource) AS DataSource,
			CASE WHEN ISNULL(G.CUM_OIL_3MO, 0) = 0 THEN NULL ELSE G.CUM_GAS_3MO * 1000.0 / G.CUM_OIL_3MO END AS GOR, 
			CASE WHEN ISNULL(W.CUM_OIL_6MO_TAIL, 0) + ISNULL(W.CUM_WATER_6MO_TAIL, 0) = 0 THEN NULL ELSE ISNULL(W.CUM_WATER_6MO_TAIL, 0) / (ISNULL(W.CUM_OIL_6MO_TAIL, 0) + ISNULL(W.CUM_WATER_6MO_TAIL, 0)) END AS WTR_CUT
FROM		GOR_RAW_CTE G
LEFT JOIN	WTR_CUT_RAW_CTE W ON G.WellID = W.WellID AND G.DataSource = W.DataSource
;
GO


